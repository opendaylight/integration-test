*** Settings ***
Documentation     Test suite for the arp handler bundle.
Suite Teardown    Delete All Sessions
Library           Collections
Library           RequestsLibrary
Library           ../../libraries/Common.py
Variables         ../../variables/Variables.py

*** Variables ***
${name}           test_userlink1
${key}            userLinks
${REST_CONTEXT}    /controller/nb/v2/topology

*** Test Cases ***
Add a userlink
    [Documentation]    Add a userlink, list to validate the result.
    [Tags]    add
    ${body}    Create Dictionary    name=${name}    status=Success
    ...     srcNodeConnector=OF|1@OF|00:00:00:00:00:00:00:02
    ...     dstNodeConnector=OF|1@OF|00:00:00:00:00:00:00:03
    ${headers}    Create Dictionary    Content-Type=application/json
    Create Session    session    http://${CONTROLLER}:8080    headers=${headers}    auth=${auth}
    ${resp}    Put    session    ${REST_CONTEXT}/${CONTAINER}/userLink/${name}    data=${body}
    Should Be Equal As Strings    ${resp.status_code}    201    Response status code error
    ${resp}    Get    session    ${REST_CONTEXT}/${CONTAINER}/userLinks
    Should Be Equal As Strings    ${resp.status_code}    200    Response status code error
    ${result}    To JSON    ${resp.content}
    ${content}    Get From Dictionary    ${result}    ${key}
    List Should Contain Value    ${content}    ${body}

Remove a userlink
    [Documentation]    Remove a userlink, list to validate the result.
    [Tags]    remove
    ${body}    Create Dictionary    name=${name}    status=Success
    ...     srcNodeConnector=OF|1@OF|00:00:00:00:00:00:00:02
    ...     dstNodeConnector=OF|1@OF|00:00:00:00:00:00:00:03
    ${headers}    Create Dictionary    Content-Type=application/json
    Create Session    session    http://${CONTROLLER}:8080    headers=${headers}    auth=${auth}
    ${resp}    Delete    session    ${REST_CONTEXT}/${CONTAINER}/userLink/${name}
    Should Be Equal As Strings    ${resp.status_code}    204    Response status code error
    ${resp}    Get    session    ${REST_CONTEXT}/${CONTAINER}/userLinks
    Should Be Equal As Strings    ${resp.status_code}    200    Response status code error
    ${result}    To JSON    ${resp.content}
    ${content}    Get From Dictionary    ${result}    ${key}
    List Should Not Contain Value    ${content}    ${body}
