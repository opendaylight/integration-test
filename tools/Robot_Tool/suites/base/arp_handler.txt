*** Settings ***
Documentation     Test suite for the arp handler module.
Suite Teardown    Delete All Sessions
Library           Collections
Library           RequestsLibrary
Library           ../../libraries/Common.py
Variables         ../../variables/Variables.py

*** Variables ***
${name}           test
${key}            subnetConfig
${REST_CONTEXT}    /controller/nb/v2/subnetservice

*** Test Cases ***
Add a subnet
    [Documentation]    Add a subnet, list to validate the result.
    [Tags]    add
    ${body}    Create Dictionary    name=${name}    subnet=10.0.0.254/8
    ${headers}    Create Dictionary    Content-Type=application/json
    Create Session    session    http://${CONTROLLER}:8080    headers=${headers}    auth=${auth}
    ${resp}    Put    session    ${REST_CONTEXT}/${CONTAINER}/subnet/${name}    data=${body}
    Should Be Equal As Strings    ${resp.status_code}    201    Response status code error
    ${resp}    Get    session    ${REST_CONTEXT}/${CONTAINER}/subnets
    Should Be Equal As Strings    ${resp.status_code}    200    Response status code error
    ${result}    To JSON    ${resp.content}
    ${content}    Get From Dictionary    ${result}    ${key}
    List Should Contain Value    ${content}    ${body}

Remove a subnet
    [Documentation]    Remove a subnet, list to validate the result.
    [Tags]    remove
    ${body}    Create Dictionary    name=${name}    subnet=10.0.0.254/8
    ${headers}    Create Dictionary    Content-Type=application/json
    Create Session    session    http://${CONTROLLER}:8080    headers=${headers}    auth=${auth}
    ${resp}    Delete    session    ${REST_CONTEXT}/${CONTAINER}/subnet/${name}
    Should Be Equal As Strings    ${resp.status_code}    204    Response status code error
    ${resp}    Get    session    ${REST_CONTEXT}/${CONTAINER}/subnets
    Should Be Equal As Strings    ${resp.status_code}    200    Response status code error
    ${result}    To JSON    ${resp.content}
    ${content}    Get From Dictionary    ${result}    ${key}
    List Should Not Contain Value    ${content}    ${body}
